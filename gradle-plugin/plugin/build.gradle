plugins {
    id 'java-gradle-plugin'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.smali:dexlib2:2.5.2'
    compileOnly 'com.android.tools.build:gradle:7.0.3'
    compileOnly gradleApi()
}

group 'com.github.raedev'

gradlePlugin {
    plugins {
        dexExclude {
            id = 'com.github.raedev.plugin.exclude'
            implementationClass = 'com.github.raedev.dex.DexExcludePlugin'
        }
    }
}
afterEvaluate {
    apply plugin: 'maven-publish'
    publishing {
        Properties properties = new Properties()
        properties.load(new FileInputStream(new File(rootProject.rootDir.getPath(), 'local.properties')))
        repositories {
            maven {
                url = properties.getProperty('mvn.url')
                allowInsecureProtocol true
                credentials {
                    username = properties.getProperty('mvn.username')
                    password = properties.getProperty('mvn.password')
                }
            }
        }
        publications {
            mavenJar(MavenPublication) {
                from components.java
                groupId = 'com.github.raedev'
                artifactId = 'DexExcludePlugin'
                version = '1.0.1'
                pom {
                    name = artifactId
                    url = 'https://github.com/raedev'
                    developers {
                        developer {
                            id = 'RAE'
                            name = 'RAE'
                            email = 'raedev@qq.com'
                        }
                    }
                }
            }
        }
    }
}
